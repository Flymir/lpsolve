include(CheckIncludeFile)
include(CheckSymbolExists)

check_include_file(float.h HAVE_FLOAT_H)
check_include_file(math.h HAVE_MATH_H)
check_include_file(ieeefp.h HAVE_IEEEFP_H)

if(HAVE_FLOAT_H)
	list(APPEND LPSOLVE_CHECK_HEADERS "float.h")
endif()
if(HAVE_MATH_H)
	list(APPEND LPSOLVE_CHECK_HEADERS "math.h")
endif()
if(HAVE_IEEEFP_H)
	list(APPEND LPSOLVE_CHECK_HEADERS "ieeefp.h")
endif()

check_symbol_exists(finite "${LPSOLVE_CHECK_HEADERS}" HAVE_FINITE)
check_symbol_exists(_finite "${LPSOLVE_CHECK_HEADERS}" HAVE__FINITE)
check_symbol_exists(isfinite "${LPSOLVE_CHECK_HEADERS}" HAVE_ISFINITE)
check_symbol_exists(isnan "${LPSOLVE_CHECK_HEADERS}" HAVE_ISNAN)
check_symbol_exists(_isnan "${LPSOLVE_CHECK_HEADERS}" HAVE__ISNAN)
check_symbol_exists(isnand "${LPSOLVE_CHECK_HEADERS}" HAVE_ISNAND)

if(HAVE_FINITE)
	set(LPSOLVE_C_FINITE finite)
elseif(HAVE__FINITE)
	set(LPSOLVE_C_FINITE _finite)
elseif(HAVE_ISFINITE)
	set(LPSOLVE_C_FINITE isfinite)
else()
	message(FATAL_ERROR "C-function for inf check is undefined")
endif()

if(HAVE_ISNAN)
	set(LPSOLVE_C_ISNAN isnan)
elseif(HAVE__ISNAN)
	set(LPSOLVE_C_ISNAN _isnan)
elseif(HAVE_ISNAND)
	set(LPSOLVE_C_ISNAN isnand)
else()
	message(FATAL_ERROR "C-function for isnan check is undefined")
endif()

set(LPSOLVE_INT64_T "long long")
set(LPSOLVE_UINT64_T "unsigned long long")
set(LPSOLVE_INTPTR_T "int *")
